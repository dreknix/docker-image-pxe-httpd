#!ipxe

# Variables are specified in boot.ipxe.cfg

# Some menu defaults
isset ${default-menu-timeout} || set default-menu-timeout 7000
set menu-timeout ${default-menu-timeout}
set submenu-timeout ${default-menu-timeout}

isset ${default-menu-target} || set default-menu-target boot
set menu-target ${default-menu-target}

###################### MAIN MENU ####################################

:start
menu iPXE menu - ${product} (${archb}bit)
item --gap --             INSTALLATION
#TODO item --key u ubuntu       (X)Ubuntu Install & Live
#TODO item --key f fedora       Fedora Install & Live
#TODO item --key w winpe10      WinPE 10 64bit
item --gap --
item --gap --             MISCELLANEOUS
item --key n netboot      netboot.xyz
item --key m memtest      Memtest86+
item --key s systemrescue SystemRescue
item --key d freedos      FreeDOS (only in BIOS mode)
item --gap --
item --gap --             ADVANCED OPTIONS
item --key c config       Configure settings
item shell                Drop to iPXE shell
item boot                 Boot from local disk
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue PXE boot
choose --timeout ${menu-timeout} --default ${menu-target} selected || goto cancel
set menu-timeout 0
goto ${selected}

###
### CANCEL
:cancel
echo You cancelled the menu, dropping you to a shell
goto shell

###
### CONFIG
:config
config
goto start

###
### SHELL
:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

###
### FAILED
:failed
echo Booting failed, dropping to shell
sleep 5
goto shell

###
### BOOT
:boot
show platform
iseq ${platform} efi && goto boot-uefi || goto boot-bios
:boot-bios
sanboot --no-describe --drive 0x80 || echo sanboot failed
goto failed
:boot-uefi
sanboot --no-describe --drive 0 || echo sanboot failed
goto failed

###
### REBOOT
:reboot
reboot

###
### EXIT
:exit
exit

###################### MAIN MENU ITEMS ##############################

###
### netboot.xyz
:netboot
iseq ${platform} efi && goto netboot-uefi || goto netboot-bios
:netboot-bios
chain --autofree https://boot.netboot.xyz/ipxe/netboot.xyz.lkrn || goto failed
goto start
:netboot-uefi
chain --autofree https://boot.netboot.xyz/ipxe/netboot.xyz.efi || goto failed
goto start

###
### Memtest86+
:memtest
iseq ${platform} efi && goto memtest-uefi || goto memtest-bios
:memtest-bios
initrd ${base-url}/iso/mt86plus_6.10_64.iso || goto failed
chain ${base-url}/iso/memdisk iso raw || goto failed
goto start
:memtest-uefi
chain ${base-url}/iso/memtest64.efi || goto failed
goto start

###
### SystemRescue
:systemrescue
# working only for BIOS system
#imgfree
#kernel ${base-url}/iso/memdisk iso raw || goto failed
#initrd --name systemrescue-9.06-amd64.iso ${base-url}/iso/systemrescue-9.06-amd64.iso || goto failed
#boot
kernel ${base-url}/iso/systemrescue/sysresccd/boot/x86_64/vmlinuz initrd=sysresccd.img archisobasedir=sysresccd ip=dhcp archiso_http_srv=${base-url}/iso/systemrescue/ checksum || goto failed
initrd ${base-url}/iso/systemrescue/sysresccd/boot/x86_64/sysresccd.img || goto failed
boot || goto failed
goto start

###
### FreeDOS
:freedos
sanboot ${base-url}/iso/freedos_1.3_live.iso || goto failed
goto start
